# entries from the Semantic Arts UoM reference data that the client will be using
# run this query on the Semantic Arts repository for Unit of Measure reference data
# note: sequence of clauses in the union is important
# 
# select distinct ?unitGroup ?unitGroupPredicate ?unitGroupObject

construct {
  ?unitOfMeasure ?unitOfMeasurePredicate ?unitOfMeasureObject .  
  ?category      ?categoryPredicate      ?categoryObject .  
  ?unitGroup     ?unitGroupPredicate     ?unitGroupObject .
  ?broaderAspect ?broaderAspectPredicate ?broaderAspectObject .
  }

where {

# input is a list of Aspects and a list of Units the client will be using

  values ?aspect {

#        gistd:_Aspect_budget       # sample value, commented out
         }
    
# reference data for the units
  {
   values ?unitOfMeasure {

#        gistd:_UnitOfMeasure_US_dollar  # sample value, commented out
         }
    
    ?unitOfMeasure ?unitOfMeasurePredicate ?unitOfMeasureObject .}
   
# reference data for the categories
  union {
  ?aspect gist:hasBroader* ?broaderAspect .
  ?broaderAspect gist:isCategorizedBy ?category .
  ?category ?categoryPredicate ?categoryObject .
  }

# reference data for the unit groups
  union {
  ?aspect gist:hasBroader* ?broaderAspect .
  ?broaderAspect gist:hasUnitGroup ?unitGroup .
  ?unitGroup ?unitGroupPredicate ?unitGroupObject .
  }
  
# reference data for the aspects and broader aspects
  union {
  ?aspect gist:hasBroader* ?broaderAspect .
  ?broaderAspect ?broaderAspectPredicate ?broaderAspectObject .
  }  
}

