prefix sh: <http://www.w3.org/ns/shacl#>
prefix gist: <https://w3id.org/semanticarts/ns/ontology/gist/>

CONSTRUCT {
?report a sh:ValidationReport ;
  sh:conforms false ;
  sh:result
    [
      a sh:ValidationResult ;
      sh:focusNode ?deleted ;
      sh:resultMessage ?error ;
      sh:resultSeverity sh:Warning ;
      sh:sourceConstraintComponent <urn:constraint:produces-in-schema>
    ] .
}
WHERE {
  VALUES ?deleted {
    # Issue 1163
    gist:produces
  }
  # Uses of gist:produces as a property will be automatically inverted
  # Uses as object/subject should be reported
  {
      graph ?g { ?s ?p ?deleted }
  } UNION {
      graph ?g { ?deleted ?p ?o }
  }
  BIND(<urn:deleted-entity-validation-report> as ?report)
  BIND(CONCAT("Removed entity gist:",
      REPLACE(STR(?deleted), '^.*[/#]', ''), " referenced in data. ",
      "Review release notes for information about the changes.") as ?error)
}
