prefix sh: <http://www.w3.org/ns/shacl#>
prefix gist: <https://w3id.org/semanticarts/ns/ontology/gist/>

CONSTRUCT {
?report a sh:ValidationReport ;
  sh:conforms false ;
  sh:result
    [
      a sh:ValidationResult ;
      sh:focusNode ?deleted ;
      sh:resultMessage ?error ;
      sh:resultSeverity sh:Warning ;
      sh:sourceConstraintComponent <urn:constraint:removed-entity>
    ] .
}
WHERE {
  VALUES ?deleted {
    # Issue 1182
    gist:GeoSegment

    # Issue 1221
    gist:Obligation

    # Issue 1194
    gist:Artifact

    # Issue 1247
    gist:accepts
  }
  {
      ?s ?deleted ?o
  } UNION {
      ?s ?p ?deleted
  } UNION {
      ?deleted ?p ?o
  }
  BIND(<urn:deleted-entity-validation-report> as ?report)
  BIND(CONCAT("Removed entity gist:",
      REPLACE(STR(?deleted), '^.*[/#]', ''), " referenced in data. ",
      "Review release notes for information about the changes.") as ?error)
}
