prefix sh: <http://www.w3.org/ns/shacl#>
prefix gist: <https://ontologies.semanticarts.com/gist/>

CONSTRUCT {
    ?report a sh:ValidationReport ;
        sh:conforms false ;
        sh:result
            [
                a sh:ValidationResult ;
                sh:focusNode ?p ;
                sh:resultMessage ?error ;
                sh:resultSeverity sh:Warning ;
                sh:sourceConstraintComponent <urn:constraint:renamed-entity>
            ] .
}
WHERE {
    {
        ?s ?p ?o 
        FILTER(STRSTARTS(STR(?p), "https://ontologies.semanticarts.com/gist/"))
    }
    bind(<urn:renamed-entity-validation-report> as ?report)
    bind(CONCAT("WARNING: old namespace in use: ", STR(?p),
        ". This should now be: https://w3id.org/semanticarts/ns/ontology/gist/", REPLACE(STR(?p), '^.*[/#]', '')
        ) as ?error)
}
