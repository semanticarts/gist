prefix sh: <http://www.w3.org/ns/shacl#>
prefix gist: <https://w3id.org/semanticarts/ns/ontology/gist/>

CONSTRUCT {
  ?report a sh:ValidationReport ;
    sh:conforms false ;
    sh:result
      [
        a sh:ValidationResult ;
        sh:focusNode ?s ;
        sh:resultMessage ?error ;
        sh:resultSeverity sh:Warning ;
        sh:sourceConstraintComponent <urn:constraint:renamed-entity>
      ] .
}
WHERE {
  GRAPH ?g {
    ?s ?p ?o
  }
  FILTER(STRSTARTS(STR(?s), "https://ontologies.semanticarts.com/gist/"))
  BIND(<urn:renamed-entity-validation-report> as ?report)
  BIND(CONCAT("WARNING: old namespace in use: ", STR(?s),
    ". This should now be: https://w3id.org/semanticarts/ns/ontology/gist/", REPLACE(STR(?s), '^.*[/#]', '')
    ) as ?error)
}
