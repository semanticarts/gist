prefix sh: <http://www.w3.org/ns/shacl#>
prefix gist: <https://ontologies.semanticarts.com/gist/>

CONSTRUCT {
  ?report a sh:ValidationReport ;
    sh:conforms false ;
    sh:result
      [
        a sh:ValidationResult ;
        sh:focusNode ?g ;
        sh:resultMessage ?error ;
        sh:resultSeverity sh:Warning ;
        sh:sourceConstraintComponent <urn:constraint:renamed-entity>
      ] .
}
WHERE {
  GRAPH ?g {
    ?s ?p ?o 
  }
  FILTER(STRSTARTS(STR(?g), "https://ontologies.semanticarts.com/gist/"))
  BIND(<urn:renamed-entity-validation-report> as ?report)
  BIND(CONCAT("WARNING: old namespace in use: ", STR(?g),
    ". This should now be: https://w3id.org/semanticarts/ns/ontology/gist/", REPLACE(STR(?g), '^.*[/#]', '')
    ) as ?error)
}
