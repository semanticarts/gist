prefix sh: <http://www.w3.org/ns/shacl#>
prefix gist: <https://ontologies.semanticarts.com/gist/>

CONSTRUCT {
    ?report a sh:ValidationReport ;
        sh:conforms false ;
        sh:result
            [
                a sh:ValidationResult ;
                sh:focusNode ?deleted ;
                sh:resultMessage ?error ;
                sh:resultSeverity sh:Warning ;
                sh:sourceConstraintComponent <urn:constraint:removed-entity>
            ] .
}
WHERE {
    {
        select distinct ?deleted where {
            values ?deleted {
              ## Issue: #785
              ## PR: #793
              gist:Percentage
              ## Issue: #650 
              ## PR: #815
              gist:TimeZone
              gist:TimeZoneStandard
              gist:hasOffsetToUniversal
              gist:usesTimeZoneStandard
              ## Issue: #506
              ## PR: #813
              gist:hasDirectPart
              gist:hasDirectSubTask
              gist:hasDirectSuperCategory
              gist:hasMember
              gist:hasNavigationalParent
              gist:hasPart
              gist:hasSubTask
              gist:hasSuperCategory
              gist:isAbout
              gist:isAffectedBy
              gist:isBasedOn
              gist:isGeographicallyContainedIn
              gist:isGovernedBy
              gist:isIdentifiedBy
              gist:isRecognizedBy
              gist:occupiesGeographically
              gist:occupiesGeographicallyPermanently
              gist:precedes
              gist:precedesDirectly
              ## Issue: #766, 756
              ## PR: #825
              gist:TreatyOrganization
            }
            {
                ?s ?deleted ?o
            } UNION {
                ?s ?p ?deleted
            } UNION {
                ?deleted ?p ?o
           } UNION {
               graph ?g { ?s ?deleted ?o }
           } UNION {
               graph ?g { ?s ?p ?deleted }
           } UNION {
               graph ?g { ?deleted ?p ?o }
          }
      }
  }
  BIND(<urn:deleted-entity-validation-report> as ?report)
  BIND(CONCAT("Removed entity gist:",
      REPLACE(STR(?deleted), '^.*[/#]', ''), " referenced in data. ",
      "Review release notes for information about the changes.") as ?error)
}